
/*
The same example from lecture 3, but using a property.
*/

range T = 1..2

Var = Var[1],
Var[u:T] =
	( read[u]   ->Var[u]
	| write[v:T]->Var[v]).

set Bool = {true,false}

BOOL(Init='false) = BOOL[Init],
BOOL[b:Bool] =
	( is[b] -> BOOL[b]
	| setTo[x:Bool] -> BOOL[x]
    ).

P1 = ( enter1.setTo['true]
	-> turn.write[2]
	-> Gd1),
Gd1 =
	( enter2.is['false] -> CS1
	| enter2.is['true] ->
		( turn.read[1] -> CS1
		| turn.read[2] -> Gd1)),
CS1 = ( a -> b
	-> enter1.setTo['false]
	-> P1).


P2 = ( enter2.setTo['true]
	-> turn.write[1]
	-> Gd2),
Gd2 =
	( enter1.is['false] -> CS2
	| enter1.is['true] ->
		( turn.read[2] -> CS2
		| turn.read[1] -> Gd2)),
CS2 = ( c -> d
	-> enter2.setTo['false]
	-> P2).

/*
OldOk = ( a -> ( c -> ERROR | b -> OldOk )
	|  c -> ( a -> ERROR | d -> OldOk)).

property Ok1 = ( a -> ( c -> ERROR | b -> Ok1 )
	|  c -> ( a -> ERROR | d -> Ok1)).
||BusyWaitOK = (enter1:BOOL||enter2:BOOL||turn:Var||P1||P2||OldOk||Ok1||Ok).
*/

property Ok = ( a -> b -> Ok
	|  c -> d -> Ok).
||BusyWaitOK = (enter1:BOOL||enter2:BOOL||turn:Var||P1||P2||Ok).
